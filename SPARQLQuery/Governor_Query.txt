PREFIX dbpedia2: <http://dbpedia.org/property/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX on: <http://dbpedia.org/ontology/>
PREFIX gephi: <http://gephi.org/>
PREFIX : <http://dbpedia.org/resource/>
PREFIX dcsub: <http://purl.org/dc/terms/>
PREFIX cat: <http://dbpedia.org/resource/Category:>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
 
CONSTRUCT {
?s gephi:label ?lstate.
?capital gephi:label ?lcapital.
?newgovernor gephi:label ?newlgovernor.
?party gephi:label ?lparty.
?gprofession gephi:label ?lgprofession.
?guniv gephi:label ?lguniv.
?ucity gephi:label ?lucity.
?uaffiliation gephi:label ?luaffiliation.
?s gephi:category "State".
?capital gephi:category "Capital City".
?newgovernor gephi:category "Governor".
?party gephi:category "Party".
?gprofession gephi:category "Profession".
?guniv gephi:category "University".
?ucity gephi:category "University City".
?uaffiliation gephi:category "Affiliation".
?s dbpedia2:capital ?capital.
?s dbpedia2:governor ?newgovernor.
?newgovernor on:party ?party.
?newgovernor on:profession ?gprofession.
?newgovernor on:birthDate ?gbday.
?newgovernor on:almaMater ?guniv.
?guniv on:city ?ucity.
?guniv on:affiliation ?uaffiliation.
}
 
where 
{
?s dcsub:subject  cat:States_of_the_United_States.
?s a on:Place. 
?s dbpedia2:capital ?capital.
?s rdfs:label ?lstate.
?capital rdfs:label ?lcapital.
?s dbpedia2:governor ?governor.
BIND(IF(ISURI(?governor),?governor,IRI(concat("<http://dbpedia.org/resource/",?governor))) as ?newgovernor).
OPTIONAL{?governor foaf:name ?lgovernor.} 
OPTIONAL{?governor on:party ?party.}
OPTIONAL{?party foaf:name ?lparty.}
OPTIONAL{?governor on:profession ?gprofession.}
OPTIONAL{?gprofession rdfs:label ?lgprofession.}
OPTIONAL{?governor on:religion ?greligion.}
OPTIONAL{?greligion rdfs:label ?lgreligion.}
OPTIONAL{?governor on:birthDate ?gbday.}
OPTIONAL{?governor on:almaMater ?guniv.}
OPTIONAL{?guniv rdfs:label ?lguniv}
OPTIONAL{?guniv on:city ?ucity.}
OPTIONAL{?ucity rdfs:label ?lucity.}
OPTIONAL{?guniv on:affiliation ?uaffiliation.}
OPTIONAL{?uaffiliation rdfs:label ?luaffiliation.}
FILTER ( LANG(?lstate) = "en" )
FILTER ( LANG(?lgprofession) = "en"  || !BOUND(?lgprofession)) 
FILTER ( LANG(?lgreligion) = "en" || !BOUND(?lgreligion)) 
FILTER ( LANG(?lguniv) = "en" || !BOUND(?lguniv) )
FILTER ( LANG(?lucity) = "en" || !BOUND(?lucity) )
FILTER ( LANG(?luaffiliation) = "en" || !BOUND(?luaffiliation))
FILTER ( LANG(?lcapital) = "en" || !BOUND(?lcapital) )
BIND( IF(BOUND(?lgovernor),?lgovernor,?governor) as ?newlgovernor)
} 
LIMIT 10000